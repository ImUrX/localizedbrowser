plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
version = version + "-forge"

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

unimined.minecraft {
    forge {
        forge rootProject.forge_version
    }
}

dependencies {
    // Remove the next line if you don't want to depend on the API
    //modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    implementation 'thedarkcolour:kotlinforforge:3.6.0'

    implementation "dev.esnault.wanakana:wanakana-core:${rootProject.wanakana_version}"
    implementation "com.atilika.kuromoji:kuromoji-unidic:${rootProject.kuromoji_version}"
    shadowCommon "dev.esnault.wanakana:wanakana-core:${rootProject.wanakana_version}"
    //shadow "com.atilika.kuromoji:kuromoji-unidic:${rootProject.kuromoji_version}"
    common(project(path: ":common")) { transitive false }
    shadowCommon(project(path: ":common")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    inputFile = shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
